plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.eclypses.relay'
    compileSdk 33

    defaultConfig {
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "3.4.7"
        setProperty("archivesBaseName", "eclypses-aws-mte-relay-client-android-$versionName")
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.android.volley:volley:1.2.1'
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                setGroupId 'com.eclypses'
                setArtifactId 'eclypses-aws-mte-relay-client-android-release'
                version '3.4.7'
                artifact bundleReleaseAar
                pom {
                    packaging = "aar"
                    name.set("MteRelay")
                    description.set("MteRelay: Library for Eclypses MteRelay")
                    url.set("https://github.com/Eclypses/eclypses-aws-mte-relay-client-android.git")
                    inceptionYear.set("2024")

                    licenses {
                        license {
                            name.set("MIT License")
                            url.set("https://opensource.org/licenses/MIT")
                        }
                    }

                    developers {
                        developer {
                            id.set("GregWaggoner")
                            name.set("Greg Waggoner")
                            email.set("greg.waggoner@eclypses.com")
                        }
                    }

                    scm {
                        connection.set("scm:git@github.com:Eclypses/eclypses-aws-mte-relay-client-android")
                        developerConnection.set("scm:git@github.com:Eclypses/eclypses-aws-mte-relay-client-android.git")
                        url.set("https://github.com/Eclypses/eclypses-aws-mte-relay-client-android.git")
                    }
                }
            }
        }
    }
}